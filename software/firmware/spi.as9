********
SPI_Init
********

        BSR     SPI_Deselect_Cards
        STA     IO_SPI_CLK_0
        RTS


***********************
SUBROUTINE SD_Card_Info
***********************

*****************************
* Input : A = Card (0 or 1) *
* Used  : W,X               *
*****************************

        LDW     #(IO_CD1 << 8) | IO_WP1 ; card 1
        TSTA
        BNE     +
        LSRW                    ; shift E & F for card 0
+       LDX     #Msg_CD         ; "Card in drive "
        CALL    PUTS
        ORA     #'0'            ; Convert # to ASCII
        CALL    PUTC
        LDA     IO_SDCARD
        ANDR    A,E             ; test IO_INCD#
        BEQ     +
        LDX     #Msg_NOT        ; " not"
        CALL    PUTS
+       LDX     #Msg_DET        ; " detected, write protect "
        CALL    PUTS
        LDX     #Msg_ON         ; "ON"
        LDA     IO_SDCARD
        ANDR    A,F             ; test IO_INWP#
        BNE     +
        LDX     #Msg_OFF        ; "OFF"
+       CALL    PUTS
        JMP     Mon_CRLF
ENDSUB


******************
SPI_Deselect_Cards
******************
; also deselects auxiliary devices SPI1 and SPI2

        PSHS    A
        LDA     #IO_SEL_SD0 | IO_SEL_SD1 | IO_SEL_SPI1 | IO_SEL_SPI2
        STA     IO_SDCARD
        PULS    A,PC


*****************
SPI_Select_Card_0
*****************

        PSHS    A
        LDA     #$FF-IO_SEL_SD0
        STA     IO_SDCARD
        PULS    A,PC


*****************
SPI_Select_Card_1
*****************

        PSHS    A
        LDA     #$FF-IO_SEL_SD1
        STA     IO_SDCARD
        PULS    A,PC


**********
SPI_CLK_HI
**********

        STA     IO_SPI_CLK_1
        RTS


**********
SPI_CLK_LO
**********

        STA     IO_SPI_CLK_0
        RTS


***********
SPI_MOSI_HI
***********

        PSHS    A
        LDA     #$80            ; bit 7
        STA     IO_SPI_DATA
        PULS    A,PC


***********
SPI_MOSI_LO
***********

        CLR     IO_SPI_DATA
        RTS


******************
SUBROUTINE SPI_XCG
******************

************************************************
* Exchange bytes over the SPI bus: writes byte *
* in A to SPI bus and reads from bus to B.     *
* A keeps its value for further writes         *
* -------------------------------------------- *
* Input : A = byte to write out over SPI bus   *
* Output: A = written byte, restored at return *
*         B = byte read in from SPI bus        *
************************************************

        LDB     #1              ; loop count marker
.loop   STA     IO_SPI_DATA     ; MOSI = bit 7
        LSLA                    ; left shift A, C = former bit 7 of A
        ADCA    #0              ; put carry into bit 0 of A
        STA     IO_SPI_CLK_1    ; SCLK = 1
        TST     IO_SPI_DATA     ; N flag = MISO
        BPL     .bit            ; branch if N==0
        ORCC    #1              ; set carry (otherwise clear from ADCA)
.bit    STA     IO_SPI_CLK_0    ; SCLK = 0
        ROLB                    ; put carry into bit 0 of B
        BCC     .loop           ; loop for 8 shifts
        RTS
ENDSUB

*******************
SUBROUTINE XCG_test
*******************

**********************************************************
* Test routine for SPI byte exchange                     *
* Prerequisite: short-circuit MISO and MOSI              *
* This routine will then send all values from $00 to $FF *
* over the SPI bus and read them back.                   *
* If a value read back differs from the sent one, the    *
* values are printed and the test aborts.                *
**********************************************************

        CLRA
.loop   TFR     A,B             ; ensure that A != B
        COMB
        JSR     SPI_XCG         ; shift bits out and in
        CMPR    A,B             ; B must be A, read in again
        BEQ     .ok
        PSHS    D               ; show A
        JSR     Mon_Show_Blank_Byte
        PULS    D
        TFR     B,A             ; show B
        JSR     Mon_Show_Blank_Byte
        JSR     Mon_CRLF
        RTS
.ok     DECA
        BNE .loop
        RTS
ENDSUB



Msg_CD  FCB "Card in drive \0"
Msg_NOT FCB " not\0"
Msg_DET FCB " detected, write protect \0"
Msg_ON  FCB "ON\0"
Msg_OFF FCB "OFF\0"


